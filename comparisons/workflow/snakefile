configfile: "workflow/config.yml"

rule all:
    input:
        "plots/faith_pd.pdf",
        "plots/shannon_entropy.pdf",
        "stats/faith_total_results.tsv",
        "stats/faith_diet_results.tsv",
        "stats/faith_dunn.tsv",
        "stats/shannon_total_results.tsv",
        "stats/shannon_diet_results.tsv",
        "stats/shannon_dunn.tsv",
        "plots/faith_stat_vis.pdf",
        "plots/shannon_stat_vis.pdf",
        "plots/unweighted_unifrac_pcoa.pdf",
        "plots/weighted_unifrac_pcoa.pdf",
        "stats/w_adonis_results.tsv",
        "stats/uw_adonis_results.tsv",
        "stats/w_adonis_by_day.tsv",
        "stats/uw_adonis_by_day.tsv",
        "stats/wu_homogeneity.tsv",
        "stats/wu_homog_dunn.tsv",
        "stats/uu_homogeneity.tsv",
        "stats/uu_homog_dunn.tsv",
        "plots/wu_homogeneity.pdf",
        "plots/wu_homog_stats.pdf",
        "plots/uu_homogeneity.pdf",
        "plots/uu_homog_stats.pdf",
        "stats/uu_resiliency.tsv",
        "stats/uu_resil_dunn.tsv",
        "stats/wu_resiliency.tsv",
        "stats/wu_resil_dunn.tsv",
        "plots/wu_resiliency.pdf",
        "plots/wu_resil_stats.pdf",
        "plots/uu_resiliency.pdf",
        "plots/uu_resil_stats.pdf",
        "plots/family_abun1.pdf",
        "plots/family_abun2.pdf",
        "stats/family_abun_lm.tsv",
        "stats/family_abun_dunn.tsv",
        "plots/famAbun_stat_vis.pdf"




rule merge_tables:
    input:
        table1 = config["table1"],
        rep_seqs1 = config["rep_seqs1"],
        table2 = config["table2"],
        rep_seqs2 = config["rep_seqs2"]
    output:
        merged_table = "data/misc/" + config["sampleType_prefix"] + "_table.qza",
        merged_seqs = "data/misc/" + config["sampleType_prefix"] + "_rep_seqs.qza"
    conda:
        "qiime2-2023.5"
    shell:
        """
        qiime feature-table merge \
            --i-tables {input.table1} \
            --i-tables {input.table2} \
            --o-merged-table {output.merged_table}
        
        qiime feature-table merge-seqs \
            --i-data {input.rep_seqs1} \
            --i-data {input.rep_seqs2} \
            --o-merged-data {output.merged_seqs}
        """


rule get_reference_databases:
    output:
        "databases/sepp-refs-silva-128.qza",
        "databases/silva-138-99-515-806-nb-classifier.qza"
    shell:
        """
        wget https://data.qiime2.org/2023.5/common/sepp-refs-silva-128.qza -P ./databases/
        wget https://data.qiime2.org/2023.5/common/silva-138-99-515-806-nb-classifier.qza -P ./databases/
        """


rule sepp_phylo_tree:
    input:
        merged_seqs = "data/misc/" + config["sampleType_prefix"] + "_rep_seqs.qza",
        silva_ref = "databases/sepp-refs-silva-128.qza"
    output:
        tree = "data/qiime/tree.qza",
        placements = "data/qiime/placements.qza"
    conda:
        "qiime2-2023.5"
    shell:
        """
        qiime fragment-insertion sepp \
            --i-representative-sequences {input.merged_seqs} \
            --i-reference-database {input.silva_ref} \
            --o-tree {output.tree} \
            --o-placements {output.placements}
        """


rule sepp_ASV_filtering:
    input:
        merged_table = "data/misc/" + config["sampleType_prefix"] + "_table.qza",
        tree = "data/qiime/tree.qza"
    output:
        filt_merged_table = "data/qiime/filt_" + config["sampleType_prefix"] + "_table.qza",
        rem_merged_table = "data/qiime/rem_" + config["sampleType_prefix"] + "_table.qza"
    conda:
        "qiime2-2023.5"
    shell:
        """
        qiime fragment-insertion filter-features \
            --i-table {input.merged_table} \
            --i-tree {input.tree} \
            --o-filtered-table {output.filt_merged_table} \
            --o-removed-table {output.rem_merged_table}
        """


rule taxonomic_classification:
    input:
        silva_class = "databases/silva-138-99-515-806-nb-classifier.qza",
        merged_seqs = "data/misc/" + config["sampleType_prefix"] + "_rep_seqs.qza"
    output:
        taxonomy = "data/qiime/taxonomy.qza"
    conda:
        "qiime2-2023.5"
    shell:
        """
        qiime feature-classifier classify-sklearn \
            --i-classifier {input.silva_class} \
            --i-reads {input.merged_seqs} \
            --o-classification {output.taxonomy}
        """

## rerun after total sum scaling
## hopefully the second qiime command works 
rule filter_taxonomy:
    input:
        filt_merged_table = "data/qiime/filt_" + config["sampleType_prefix"] + "_table.qza",
        taxonomy = "data/qiime/taxonomy.qza"
    output:
        tax_filt_table = "data/qiime/taxonomy_filtered.qza",
        tax_filt_vis = "data/qiime/taxonomy_filtered.qzv"
    conda:
        "qiime2-2023.5"
    shell:
        """
        qiime taxa filter-table \
            --i-table {input.filt_merged_table} \
            --i-taxonomy {input.taxonomy} \
            --p-include p_ \
            --p-exclude mitochondria,chloroplast \
            --o-filtered-table {output.tax_filt_table}
        
        qiime feature-table summarize \
            --i-table {output.tax_filt_table} \
            --o-visualization {output.tax_filt_vis}
        """


rule create_lacto_table:
    input:
        filt_merged_table = "data/qiime/filt_" + config["sampleType_prefix"] + "_table.qza",
        taxonomy = "data/qiime/taxonomy.qza",
        merged_seqs = "data/misc/" + config["sampleType_prefix"] + "_rep_seqs.qza"
    output:
        lacto_table = "data/qiime/lacto_table.qza",
        lacto_rep_seqs = "data/qiime/lacto_rep_seqs.qza" 
    conda:
        "qiime2-2023.5"
    shell:
        """
        qiime taxa filter-table \
            --i-table {input.filt_merged_table} \
            --i-taxonomy {input.taxonomy} \
            --p-include Lactococcus \
            --o-filtered-table {output.lacto_table}
        
        qiime feature-table filter-seqs \
            --i-data {input.merged_seqs} \
            --i-table {output.lacto_table} \
            --o-filtered-data {output.lacto_rep_seqs}
        """


rule convert_to_fasta:
    input:
        lacto_rep_seqs = "data/qiime/lacto_rep_seqs.qza"
    output:
        output_path = "data/qiime/dna-sequences.fasta",
        lacto_fasta = "data/qiime/lactoOnly_rep_seqs.fasta"
    conda:
        "qiime2-2023.5"
    shell:
        """
        qiime tools export \
            --input-path {input.lacto_rep_seqs} \
            --output-path {output.output_path}
        
        mv {output.output_path} {output.lacto_fasta}
        """


rule total_sum_scaling:
    input:
        filt_merged_table = "data/qiime/filt_" + config["sampleType_prefix"] + "_table.qza",
        asvs = "data/qiime/lactoOnly_rep_seqs.fasta"
    output:
        table = "data/misc/" + config["sampleType_prefix"] + "_total_sum_scaling.tsv"
    conda:
        "r_env"
    shell:
        """
        Rscript scripts/total_sum_scaling.R --biom {input.filt_merged_table} \
                                            --sequence {input.asvs} \
                                            --output {output.table}
        """


rule tss_tsv2biom:
    input:
        "data/misc/" + config["sampleType_prefix"] + "_total_sum_scaling.tsv"
    output:
        "data/misc/" + config["sampleType_prefix"] + "_total_sum_scaling.biom"
    conda:
        "qiime2-2023.5"
    shell:
        """
        biom convert \
            -i {input} \
            -o {output} \
            --table-type "Table" \
            --to-hdf5
        """


rule tss_biom2qza:
    input:
        "data/misc/" + config["sampleType_prefix"] + "_total_sum_scaling.biom"
    output:
        "data/misc/" + config["sampleType_prefix"] + "_total_sum_scaling.qza"
    conda:
        "qiime2-2023.5"
    shell:
        """
        qiime tools import \
            --input-path {input} \
            --type 'FeatureTable[Frequency]' \
            --output-path {output}
        """


rule sepp_ASV_filtering2:
    input:
        tss_table = "data/misc/" + config["sampleType_prefix"] + "_total_sum_scaling.qza",
        tree = "data/qiime/tree.qza"
    output:
        tss_filt_table = "data/qiime/" + config["sampleType_prefix"] + "_tss_filt_table.qza",
        tss_rem_table = "data/qiime/" + config["sampleType_prefix"] + "_tss_rem_table.qza"
    conda:
        "qiime2-2023.5"
    shell:
        """
        qiime fragment-insertion filter-features \
            --i-table {input.tss_table} \
            --i-tree {input.tree} \
            --o-filtered-table {output.tss_filt_table} \
            --o-removed-table {output.tss_rem_table}
        """


rule filter_taxonomy2:
    input:
        tss_table = "data/misc/" + config["sampleType_prefix"] + "_total_sum_scaling.qza",
        taxonomy = "data/qiime/taxonomy.qza"
    output:
        tss_tax_filt = "data/qiime/tss_tax_filt.qza",
        tss_tax_filt_vis = "data/qiime/tss_tax_filt.qzv"
    conda:
        "qiime2-2023.5"
    shell:
        """
        qiime taxa filter-table \
            --i-table {input.tss_table} \
            --i-taxonomy {input.taxonomy} \
            --p-include p_ \
            --p-exclude mitochondria,chloroplast \
            --o-filtered-table {output.tss_tax_filt}
        
        qiime feature-table summarize \
            --i-table {output.tss_tax_filt} \
            --o-visualization {output.tss_tax_filt_vis}
        """


rule pre_core_metrics_filter:
    input:
        tss_tax_filt = "data/qiime/tss_tax_filt.qza",
        metadata = config["metadata"]
    output:
        otu_table = "data/qiime/" + config["sampleType_prefix"] + "otu_table.qza"
    conda:
        "qiime2-2023.5"
    shell:
        """
        qiime feature-table filter-samples \
            --i-table {input.tss_tax_filt} \
            --m-metadata-file {input.metadata} \
            --o-filtered-table {output.otu_table}
        """


rule core_metrics_analysis:
##  NEED TO CHECK THE SAMPLING DEPTH BEFORE YOU RUN THIS STEP
    input:
        tree = "data/qiime/tree.qza",
        otu_table = "data/qiime/" + config["sampleType_prefix"] + "otu_table.qza",
        metadata = config["metadata"]
    output:
        output_dir = directory("data/core_outputs")
    conda:
        "qiime2-2023.5"
    params:
        sampling_depth=config["core_metrics_sampling_depth"]
    shell:
        """
        qiime diversity core-metrics-phylogenetic \
            --i-phylogeny {input.tree} \
            --i-table {input.otu_table} \
            --p-sampling-depth  {params.sampling_depth} \
            --m-metadata-file {input.metadata} \
            --output-dir {output}
        """


rule unzip_uw_distance_matrix:
    input:
        "data/core_outputs/unweighted_unifrac_distance_matrix.qza" 
    output:
        "data/core_outputs/uw_dist_matrix.tsv"
    conda:
        "qiime2-2023.5"
    shell:
        """
        qiime tools export \
            --input-path ./data/core_outputs/unweighted_unifrac_distance_matrix.qza \
            --output-path ./data/core_outputs/uw_dist_matrix
        
        mv ./data/core_outputs/uw_dist_matrix/distance-matrix.tsv \
        ./data/core_outputs/uw_dist_matrix/uw_dist_matrix.tsv

        mv ./data/core_outputs/uw_dist_matrix/uw_dist_matrix.tsv \
        ./data/core_outputs/
        """


rule unzip_w_distance_matrix:
    input:
        "data/core_outputs/weighted_unifrac_distance_matrix.qza"
    output:
        "data/core_outputs/w_dist_matrix.tsv"
    conda:
        "qiime2-2023.5"
    shell:
        """
        qiime tools export \
            --input-path ./data/core_outputs/weighted_unifrac_distance_matrix.qza \
            --output-path ./data/core_outputs/w_dist_matrix
        
        mv ./data/core_outputs/w_dist_matrix/distance-matrix.tsv \
        ./data/core_outputs/w_dist_matrix/w_dist_matrix.tsv

        mv ./data/core_outputs/w_dist_matrix/w_dist_matrix.tsv \
        ./data/core_outputs/ 
        """


rule unzip_shannon:
    input:
        "data/core_outputs/shannon_vector.qza"
    output:
        "data/core_outputs/shannon_entropy.tsv"
    conda:
        "qiime2-2023.5"
    shell:
        """
        qiime tools export \
            --input-path ./data/core_outputs/shannon_vector.qza \
            --output-path ./data/core_outputs/shannon_entropy
        
        mv ./data/core_outputs/shannon_entropy/alpha-diversity.tsv \
        ./data/core_outputs/shannon_entropy/shannon_entropy.tsv

        mv ./data/core_outputs/shannon_entropy/shannon_entropy.tsv \
        ./data/core_outputs/
        """


rule unzip_faith_pd:
    input:
        "data/core_outputs/faith_pd_vector.qza"
    output:
        "data/core_outputs/faith_pd.tsv"
    conda:
        "qiime2-2023.5"
    shell:
        """
        qiime tools export \
            --input-path ./data/core_outputs/faith_pd_vector.qza \
            --output-path ./data/core_outputs/faith_pd
        
        mv ./data/core_outputs/faith_pd/alpha-diversity.tsv \
        ./data/core_outputs/faith_pd/faith_pd.tsv

        mv ./data/core_outputs/faith_pd/faith_pd.tsv \
        ./data/core_outputs/
        """


rule alpha_diversity_plots:
    input:
        metadata = config["metadata"],
        faith_pd = "data/core_outputs/faith_pd.tsv",
        shannon = "data/core_outputs/shannon_entropy.tsv"
    output:
        faith_plot = "plots/faith_pd.pdf",
        shannon_plot = "plots/shannon_entropy.pdf"
    conda:
        "r_env"
    shell:
        """
        Rscript scripts/alpha_div_plots.R --metadata {input.metadata} \
                                          --faith_pd {input.faith_pd} \
                                          --shannon {input.shannon} \
                                          --output_faith {output.faith_plot} \
                                          --output_shannon {output.shannon_plot}
        """


rule alpha_diversity_stats:
    input:
        metadata = config["metadata"],
        faith_pd = "data/core_outputs/faith_pd.tsv",
        shannon = "data/core_outputs/shannon_entropy.tsv"
    output:
        faith_lm = "stats/faith_total_results.tsv",
        faith_lm_sec = "stats/faith_diet_results.tsv",
        faith_dunn = "stats/faith_dunn.tsv",
        shannon_lm = "stats/shannon_total_results.tsv",
        shannon_lm_sec = "stats/shannon_diet_results.tsv",
        shannon_dunn = "stats/shannon_dunn.tsv",
        faith_plot = "plots/faith_stat_vis.pdf",
        shannon_plot = "plots/shannon_stat_vis.pdf"
    conda:
        "r_env"
    shell:
        """
        Rscript scripts/alpha_div_stats.R --metadata {input.metadata} \
                                          --faith_pd {input.faith_pd} \
                                          --shannon {input.shannon} \
                                          --faith_lm {output.faith_lm} \
                                          --faith_lm_sec {output.faith_lm_sec} \
                                          --faith_dunn {output.faith_dunn} \
                                          --shannon_lm {output.shannon_lm} \
                                          --shannon_lm_sec {output.shannon_lm_sec} \
                                          --shannon_dunn {output.shannon_dunn} \
                                          --faith_plot {output.faith_plot} \
                                          --shannon_plot {output.shannon_plot}
        """


rule beta_diversity_plots:
    input:
        metadata = config["metadata"],
        unweighted_uni = "data/core_outputs/unweighted_unifrac_pcoa_results.qza",
        weighted_uni = "data/core_outputs/weighted_unifrac_pcoa_results.qza",
        faith_pd = "data/core_outputs/faith_pd.tsv",
        shannon = "data/core_outputs/shannon_entropy.tsv"
    output:
        unweighted_plot = "plots/unweighted_unifrac_pcoa.pdf",
        weighted_plot = "plots/weighted_unifrac_pcoa.pdf"
    conda:
        "r_env"
    shell:
        """
        Rscript scripts/beta_div_plots.R --metadata {input.metadata} \
                                         --unweighted_unifrac {input.unweighted_uni} \
                                         --weighted_unifrac {input.weighted_uni} \
                                         --faith_pd {input.faith_pd} \
                                         --shannon {input.shannon} \
                                         --output_uu {output.unweighted_plot} \
                                         --output_wu {output.weighted_plot}
        """


rule beta_diversity_stats:
    input:
        metadata = config["metadata"],
        uw_dist = "data/core_outputs/uw_dist_matrix.tsv",
        w_dist = "data/core_outputs/w_dist_matrix.tsv"
    output:
        w_adonis = "stats/w_adonis_results.tsv",
        uw_adonis = "stats/uw_adonis_results.tsv",
        w_adonis_day = "stats/w_adonis_by_day.tsv",
        uw_adonis_day = "stats/uw_adonis_by_day.tsv"
    conda:
        "r_env"
    shell:
        """
        Rscript scripts/beta_div_stats.R --metadata {input.metadata} \
                                         --uu_dist {input.uw_dist} \
                                         --wu_dist {input.w_dist} \
                                         --uu_adonis {output.uw_adonis} \
                                         --wu_adonis {output.w_adonis} \
                                         --uu_adonis_day {output.uw_adonis_day} \
                                         --wu_adonis_day {output.w_adonis_day}
        """


rule homogeneity:
    input:
        metadata = config["metadata"],
        uu_dist = "data/core_outputs/uw_dist_matrix.tsv",
        wu_dist = "data/core_outputs/w_dist_matrix.tsv"
    output:
        wu_lm = "stats/wu_homogeneity.tsv",
        wu_dunn = "stats/wu_homog_dunn.tsv",
        uu_lm = "stats/uu_homogeneity.tsv",
        uu_dunn = "stats/uu_homog_dunn.tsv",
        wu_plot = "plots/wu_homogeneity.pdf",
        wu_stat_plot = "plots/wu_homog_stats.pdf",
        uu_plot = "plots/uu_homogeneity.pdf",
        uu_stat_plot = "plots/uu_homog_stats.pdf"
    conda:
        "r_env"
    shell:
        """
        Rscript scripts/homog_calc.R --metadata {input.metadata} \
                                     --uu_dist {input.uu_dist} \
                                     --wu_dist {input.wu_dist} \
                                     --weighted_lm {output.wu_lm} \
                                     --weighted_dunn {output.wu_dunn} \
                                     --unweighted_lm {output.uu_lm} \
                                     --unweighted_dunn {output.uu_dunn} \
                                     --weighted_plot {output.wu_plot} \
                                     --weighted_stat_plot {output.wu_stat_plot} \
                                     --unweighted_plot {output.uu_plot} \
                                     --unweighted_stat_plot {output.uu_stat_plot}
        """


rule resiliency:
    input:
       metadata = config["metadata"],
       uu_dist = "data/core_outputs/uw_dist_matrix.tsv",
       wu_dist = "data/core_outputs/w_dist_matrix.tsv"
    output:
        uu_lm = "stats/uu_resiliency.tsv",
        uu_dunn = "stats/uu_resil_dunn.tsv",
        wu_lm = "stats/wu_resiliency.tsv",
        wu_dunn = "stats/wu_resil_dunn.tsv",
        wu_plot = "plots/wu_resiliency.pdf",
        wu_stat_plot = "plots/wu_resil_stats.pdf",
        uu_plot = "plots/uu_resiliency.pdf",
        uu_stat_plot = "plots/uu_resil_stats.pdf"
    conda:
        "r_env"
    shell:
        """
        Rscript scripts/resil_calc.R --metadata {input.metadata} \
                                     --uu_dist {input.uu_dist} \
                                     --wu_dist {input.wu_dist} \
                                     --weighted_lm {output.wu_lm} \
                                     --weighted_dunn {output.wu_dunn} \
                                     --unweighted_lm {output.uu_lm} \
                                     --unweighted_dunn {output.uu_dunn} \
                                     --weighted_plot {output.wu_plot} \
                                     --weighted_stat_plot {output.wu_stat_plot} \
                                     --unweighted_plot {output.uu_plot} \
                                     --unweighted_stat_plot {output.uu_stat_plot}
        """


rule family_abundance_plots:
    input:
        otu_table = "data/qiime/" + config["sampleType_prefix"] + "otu_table.qza",
        taxonomy = "data/qiime/taxonomy.qza",
        metadata = config["metadata"]
    output:
        plot1 = "plots/family_abun1.pdf",
        plot2 = "plots/family_abun2.pdf"
    conda:
        "r_env"
    shell:
        """
        Rscript scripts/family_abun_plots.R --metadata {input.metadata} \
                                            --otu {input.otu_table} \
                                            --taxonomy {input.taxonomy} \
                                            --plot1 {output.plot1} \
                                            --plot2 {output.plot2}
        """


rule family_abundance_stats:
    input:
       otu_table = "data/qiime/" + config["sampleType_prefix"] + "otu_table.qza",
       taxonomy = "data/qiime/taxonomy.qza",
       metadata = config["metadata"]
    output:
        lm = "stats/family_abun_lm.tsv",
        dunn = "stats/family_abun_dunn.tsv",
        stat_plot = "plots/famAbun_stat_vis.pdf"
    conda:
        "r_env"
    shell:
        """
        Rscript scripts/family_abun_stats.R --metadata {input.metadata} \
                                            --otu {input.otu_table} \
                                            --taxonomy {input.taxonomy} \
                                            --stat_plot {output.stat_plot} \
                                            --linear_model {output.lm} \
                                            --dunn {output.dunn}
        """

